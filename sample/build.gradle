apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply from: "../ktlint.gradle"

def keystorePropertiesFile = rootProject.file("keystore.properties")

if (keystorePropertiesFile.exists()) {

    def key = new Properties()

    key.load(new FileInputStream(keystorePropertiesFile))

    android.signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        config {
            keyAlias key['alias']
            keyPassword key['key']
            storePassword key['store']
            storeFile file(key['file'])
        }
    }

}

android {
    compileSdk 35
    defaultConfig {
        signingConfig signingConfigs.debug
        applicationId "io.github.catlandor.imagepicker.sample"
        minSdkVersion 21
        targetSdkVersion 35
        multiDexEnabled true
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.config
            }
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding true
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    lintOptions{
        disable 'MissingTranslation'
    }
    namespace 'io.github.catlandor.imagepicker.sample'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.10"

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'com.google.android.material:material:1.12.0'

    // Image Loading Lib
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    // Leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    implementation project(':imagepicker')

    testImplementation 'junit:junit:4.13.2'

    // AndroidX Test - Instrumented testing
    // Core library
    androidTestImplementation 'androidx.test:core:1.6.1'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.6.1'

    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    androidTestImplementation 'androidx.test:monitor:1.7.2'

    // For ActivityScenario
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.2.1'
}
