import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.AndroidMultiVariantLibrary

plugins {
    id "com.android.library"
    id "kotlin-android"
    id "com.vanniktech.maven.publish" version "0.31.0-rc2"
}

apply from: "../ktlint.gradle"

android {
    compileSdk 35
    defaultConfig {
        multiDexEnabled true
        minSdkVersion 21
        targetSdkVersion 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug{

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    lintOptions{
        disable 'MissingTranslation'
    }
    namespace 'io.github.catlandor.imagepicker'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.exifinterface:exifinterface:1.4.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.fragment:fragment-ktx:1.8.6'

    //More Info: https://github.com/jens-muenker/uCrop-n-Edit
    implementation 'com.github.jens-muenker:uCrop-n-Edit:4.1.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

mavenPublishing {
    // the first parameter represents whether to publish a sources jar
    // the second whether to publish a javadoc jar
    configure(new AndroidMultiVariantLibrary(true, true))

    // when publishing to https://central.sonatype.com/
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    signAllPublications()

    coordinates(PUBLISH_GROUP_ID, PUBLISH_ARTIFACT_ID, rootProject.ext.versionName)

    pom {
        name = PUBLISH_ARTIFACT_ID
        description = libraryDescription
        url = siteUrl
        licenses {
            license {
                name = licenseName
                url = licenseUrl
                distribution = licenseDistribution
            }
        }
        developers {
            developer {
                id = developerId
                url = developerEmail
            }
        }
        scm {
            url = siteUrl
            connection = gitConnection
            developerConnection = sshGitConnection
        }
    }
}